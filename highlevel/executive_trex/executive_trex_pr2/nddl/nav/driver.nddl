#include "nav/imports.nddl"
#include "nav/exports.nddl"


class NavController extends AgentTimeline{
  predicate Holds{
    bool active;
  }

  NavController(){
    super(Internal, "Holds");
  }
};

MoveBehavior driver = new MoveBehavior(Internal, true);
NavController nav_controller = new NavController();
MoveBase move_base = new MoveBase(External);

MoveBehavior::Active{
  starts(NavController.Holds nc);
  eq(nc.active, true);
}

NavController::Holds{
  met_by(Holds p);
  defaultOnCommit(active, p.active);

  if(active == true){
    contained_by(driver.Active d);
    starts_during(mobility_status.Holds ms);

    if(ms.arms_tucked == false){
      contains_start(safety_tuck_arms.Active cmd);
      concurrent(cmd.end, end);
    }
    else {
      contains_start(move_base.Active cmd);
      concurrent(cmd.end, end);
      eq_pose_msg(cmd.object, d.object);
    }
  }
  else {
    contained_by(driver.Inactive);
  }
}

MoveBase::Inactive{
  if(status == SUCCESS){
    starts(nav_controller.Holds nc);
    eq(nc.active, false);
    starts(driver.Inactive d);
    eq(d.status, SUCCESS);
  }
}
